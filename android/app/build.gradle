buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    signingConfigs {
        debug {
            keyAlias 'livecentre'
            keyPassword 'livecentre'
            storeFile file('debug.keystore')
            storePassword 'livecentre'
        }
        release {
            keyAlias 'livecentre'
            keyPassword 'livecentre'
            storeFile file('release.keystore')
            storePassword 'livecentre'
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    def build_number = System.getenv("BUILD_NUMBER") ?: "1"

    defaultConfig {
        applicationId "com.futureuniverse.livecentre"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode build_number as Integer
        versionName "1." + build_number
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }
    productFlavors {
        development {
            applicationIdSuffix ".dev"
            versionNameSuffix "-development"
            resValue "string", "app_name", "Live Centre Dev"
        }
        qa {
            applicationIdSuffix ".qa"
            versionNameSuffix "-QA"
            resValue "string", "app_name", "Live Centre QA"
        }
        staging {
            applicationIdSuffix ".stage"
            versionNameSuffix "-staging"
            resValue "string", "app_name", "Live Centre Stage"
        }
        production {
            resValue "string", "app_name", "Live Centre"
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.android.support:design:25.3.1'
    compile 'org.xwalk:xwalk_core_library:23.53.589.4'
    testCompile 'junit:junit:4.12'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.3.1'
            }
        }
    }
}
